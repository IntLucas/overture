class HolSelect is subclass of HolExpression

types
public QuantifierType = <universal> | <existential>

instance variables
variable :		HolIdentifier;
restrictor :	HolExpression

operations

public HolSelect : HolIdentifier * seq of HolExpression ==> HolSelect
HolSelect(newVar, newRestrictors) ==
(
		variable := newVar;
		restrictor := mergeExpression(newRestrictors)
)
;

public setTypeInformation : HolTypeDescription ==> ()
setTypeInformation (typeInfo) ==
	restrictor.setTypeInformation(typeInfo);

public requires : () ==> set of seq of char
requires() == 
	return restrictor.requires() \ {variable.print()};

public print : () ==> seq of char
print () ==
	return conc ["(@ ", variable.print(), ".", restrictor.print(), ")"];

functions

mergeExpression : seq of HolExpression -> HolExpression
mergeExpression (exprs) ==
    if      len exprs = 0 then new HolIdentifier("T")
    elseif  len exprs = 1 then exprs(1)
    else    new HolApplication(new HolNativeIdentifier("$/\\"), [hd(exprs), mergeExpression(tl(exprs))])
end HolSelect
