
\begin{vdm_al}
class AstUnionType is subclass of AstType

instance variables
  private lhs : AstType;
  private rhs : AstType
  
operations
  public AstUnionType: AstType * AstType ==> AstUnionType
  AstUnionType (plhs, prhs) == ( lhs := plhs; rhs := prhs );
  
  public getLhs: () ==> AstType
  getLhs () == return lhs;
  
  public getRhs: () ==> AstType
  getRhs () == return rhs;
  
  public flatten: () ==> set of AstType
  flatten () ==
    ( dcl res : set of AstType := {};
      if lhs.isUnionType()
      then ( dcl aut : AstUnionType := lhs;
             res := res union aut.flatten() )
      else res := res union {lhs};
      if rhs.isUnionType()
      then ( dcl aut : AstUnionType := rhs;
             res := res union aut.flatten() )
      else res := res union {rhs};
      return res );
      
  public accept: AstVisitor ==> ()
  accept (pv) == pv.visit(self);
  
  public isUnionType: () ==> bool
  isUnionType () == return true
    
end AstUnionType
\end{vdm_al}
