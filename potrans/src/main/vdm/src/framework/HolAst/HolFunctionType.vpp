class HolFunctionType is subclass of HolType

instance variables
domain:	HolProductType;
range:	HolType


operations

public HolFunctionType : HolProductType * HolType ==> HolFunctionType
HolFunctionType(newDom, newRng) ==
(
	domain := newDom;
	range := newRng
);

public HolFunctionType : seq of HolType * HolType ==> HolFunctionType
HolFunctionType(newDom, newRng) ==
(
	domain := new HolProductType(newDom);
	range := newRng
);

public setTypeInformation : HolTypeDescription ==> ()
setTypeInformation (typeInfo) ==
(
	domain.setTypeInformation(typeInfo);
	range.setTypeInformation(typeInfo)
);

public requires : () ==> set of seq of char
requires() == 
	return domain.requires() union range.requires();

public print : HolType`HolTypeSpecificationType ==> seq of char
print (specType) ==	
	return conc [domain.printAsCurriedFunction(specType), "->", range.print(specType)]
;

public printAsTerm : () ==> seq of char
printAsTerm () ==	
	return print(<term>)
;

public getDomain : () ==> HolProductType
getDomain() == 
	return domain
;

public getRange : () ==> HolType
getRange() == 
	return range
;

end HolFunctionType
